#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьИСоздать(Команда)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Интервал = 1;
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительнаяОперация = ЗаполнитьИСоздатьНаСервере();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОповещениеОЗавершении", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Статус = "Выполнено" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Документы созданы");	
		Данные = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		Объект.Договоры.Очистить();
		
		Для Каждого Запись Из Данные Цикл
			НоваяСтрока = Объект.Договоры.Добавить();
			НоваяСтрока.Договор = Запись.Договор;
			НоваяСтрока.Реализация = Запись.Реализация;	
		КонецЦикла;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибка");
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Функция ЗаполнитьИСоздатьНаСервере()
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, "Обработки.ВКМ_МассовоеСозданиеАктов.ФормированиеРеализаций", Объект.Период);
	
	Возврат ДлительнаяОперация;

КонецФункции

#КонецОбласти
