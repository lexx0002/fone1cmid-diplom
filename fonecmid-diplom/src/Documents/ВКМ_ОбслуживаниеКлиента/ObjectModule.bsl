#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка,
		|	ДоговорыКонтрагентов.ВидДоговора,
		|	ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействия КАК ДатаНачала,
		|	ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействия КАК ДатаОкончания,
		|	ДоговорыКонтрагентов.ВКМ_СтоимостьЧаса КАК СтоимостьЧаса
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Договор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если НЕ (Выборка.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание 
	И Дата <= Выборка.ДатаОкончания И Дата >= Выборка.ДатаНачала) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	
	КоличествоЧасов = 0;
	
	Для Каждого ТекСтрока Из ВыполненныеРаботы Цикл
		КоличествоЧасов = КоличествоЧасов + ТекСтрока.ЧасыКОплатеКлиенту;
	КонецЦикла;
	
	Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
	Движение.Период = Дата;
	Движение.Клиент = Клиент;
	Движение.Договор = Договор;
	Движение.КоличествоЧасов = КоличествоЧасов;
	Движение.СуммаКОплате = КоличествоЧасов * Выборка.СтоимостьЧаса;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период, Сотрудник = &Сотрудник) КАК
	|		ВКМ_УсловияОплатыСотрудниковСрезПоследних";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Сотрудник", Специалист);
	ВыборкаОплаты = Запрос.Выполнить().Выбрать();
	ВыборкаОплаты.Следующий();
	ПроцентОтРабот = ВыборкаОплаты.ПроцентОтРабот;
	
	Если ПроцентОтРабот <> Неопределено Тогда
		Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
		Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = Специалист;
		Движение.ЧасовКОплате = КоличествоЧасов;
		Движение.СуммаКОплате = КоличествоЧасов * Выборка.СтоимостьЧаса * ПроцентОтРабот / 100;
	Иначе
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю("На данный период сотруднику не установлен процент оплаты от работ");
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли